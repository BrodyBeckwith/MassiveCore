import org.apache.tools.ant.filters.ReplaceTokens

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.github.johnrengelman.shadow' version '5.2.0'
}

group = 'com.massivecraft.massivecore'
version = '2.15.2-SNAPSHOT'
description = 'MassiveCore is a plugin that contains libraries and features that other plugins make use of.'

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

repositories {
    mavenLocal()
    mavenCentral()
    maven { url = 'https://hub.spigotmc.org/nexus/content/repositories/snapshots' }
    maven { url = 'https://jitpack.io' }
}

dependencies {
    implementation 'org.spigotmc:spigot:1.15.2-R0.1-SNAPSHOT'
    implementation 'org.spigotmc:spigot-api:1.15.2-R0.1-SNAPSHOT'
    implementation 'com.github.MilkBowl:VaultAPI:1.7'
    shadow 'org.mongodb:mongo-java-driver:3.11.0'
}

shadowJar {
    configurations = [project.configurations.shadow]
    relocate 'com.mongodb', 'com.massivecraft.massivecore.xlib.mongodb'
    relocate 'org.bson', 'com.massivecraft.massivecore.xlib.bson'
}

tasks.build.dependsOn(tasks.shadowJar)

tasks.shadowJar.configure {
    classifier = null
}

publishing {
    publications {
        shadow(MavenPublication) { publication ->
            project.shadow.component(publication)
        }
    }
}

processResources {
    filter ReplaceTokens, tokens:[
            "project.name": project.property("name"),
            "project.group": project.property("group"),
            "project.description": project.property("description"),
            "project.version": project.property("version")
    ]
}